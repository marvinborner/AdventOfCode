import io/error
import io/filesystem
import scanner

type Direction { Left(); Right() }
record Rotation(direction: Direction, amount: Int)
effect Password = read[Rotation]

def parse(): Unit / { Scan[Char], emit[Rotation] } = {
  with on[WrongFormat].panic
  with exhaustively

  val ch = read()
  val direction = ch match {
    case 'L' => Left()
    case 'R' => Right()
    case _   => wrongFormat()
  }
  val amount = readInteger()
  skipWhitespace()
  do emit[Rotation](Rotation(direction, amount))
}

effect Solution(part1: Int, part2: Int): Unit
effect Click(at: Int): Unit
effect Final(at: Int): Unit

def betterMod(a: Int, b: Int) = a - floor(a.toDouble / b.toDouble) * b

def rotate(state: Int, direction: Direction, amount: Int): Int = (direction match {
  case Left()  => state - amount
  case Right() => state + amount
}).betterMod(100)

def enterPassword(): Unit / { Password, Click, Final } = {
  var state = 0

  with exhaustively
  val Rotation(direction, amount) = do read()
  each(0, amount) { i =>
    do Click(rotate(state, direction, i))
  }
  state = rotate(state, direction, amount)
  do Final(state)
}

def solver() { enter: => Unit / { Click, Final } }: Unit / Solution = {
  var part1 = 0
  var part2 = 0

  try enter()
  with Final { n => resume(if (n == 0) part1 = part1 + 1) }
  with Click { n => resume(if (n == 0) part2 = part2 + 1) }

  do Solution(part1, part2 - 1)
}

def printer() { solver: => Unit / Solution }: Unit = {
  try solver()
  with Solution { (part1, part2) =>
    println("Part 1: ${part1.show}")
    println("Part 2: ${part2.show}")
  }
}

def main() = {
  with on[IOError].panic
  with readFile("input")
  with decodeUTF8

  with printer
  with solver
  with source[Rotation] {
    do emit(Rotation(Right(), 50))
    scanner[Char] { parse() }
  }
  enterPassword()
}